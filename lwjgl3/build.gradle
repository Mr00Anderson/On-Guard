buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "org.beryx:badass-runtime-plugin:1.8.5"
	}
}

apply plugin: 'application'
apply plugin: 'org.beryx.runtime'

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
mainClassName = 'dev.lyze.retro.lwjgl3.Lwjgl3Launcher'
sourceCompatibility = 11

dependencies {
	implementation project(':core')
	implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"

	implementation "org.projectlombok:lombok:$lombok"
	annotationProcessor "org.projectlombok:lombok:$lombok"
}

import org.gradle.internal.os.OperatingSystem

run {
	workingDir = rootProject.file('assets').path
	setIgnoreExitValue(true)
	
	if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
		// Required to run LWJGL3 Java apps on MacOS
		jvmArgs += "-XstartOnFirstThread"
	}
}
jar {
	destinationDirectory = file("$buildDir/lib")
	archiveFileName = "${appName}-${archiveVersion.get()}.jar"
	from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } } 
	manifest {
		attributes 'Main-Class': project.mainClassName
	}
}

jpackageImage.dependsOn jar

runtime {
	options = ['--strip-debug',
			   '--compress', '2',
			   '--no-header-files',
			   '--no-man-pages',
			   '--strip-native-commands',
			   '--vm', 'server']
	modules = ['java.base' ,
			   'java.desktop',
			   'jdk.unsupported']

	jpackage {
		installerOptions += [ '--vendor', 'lyze.dev', '--description', 'OnGuard - Libgdx GameJam June 2020' ]
		mainJar = jar.archiveFileName.get()
		installerName = imageName = appName

		if (OperatingSystem.current().windows) {
			//imageOptions += [ "--icon", file("../assets/icons/Icon.ico") ]
			installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut', '--win-upgrade-uuid', '93757012-479c-4864-91c8-c47b25c7add8']
		} else {
			//imageOptions += ["--icon", file("../assets/icons/Icon-32.png")]
			installerOptions += ['--linux-menu-group', '--linux-shortcut']
		}
	}
}

